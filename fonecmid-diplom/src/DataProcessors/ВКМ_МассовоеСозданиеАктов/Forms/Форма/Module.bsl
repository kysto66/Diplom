
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокДоговоров = ПолучитьДоговорыКонтрагентов();
	
	Если СписокДоговоров.Количество() > 0 Тогда
		
		Для Каждого Договор Из СписокДоговоров Цикл
			
			СтрокаТЧ = Объект.ВКМ_СписокДоговоров.Добавить();
			СтрокаТЧ.ВКМ_Договор = Договор;	
			
		КонецЦИкла;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДоговорыКонтрагентов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	НЕ ДоговорыКонтрагентов.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокДоговоров = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		СписокДоговоров.Добавить(Выборка.Ссылка);
		
	КонецЦикла;
		
	Возврат СписокДоговоров;
	
КонецФункции

&НаКлиенте
Процедура Заполнить(Команда)
	
	ДлительнаяОперация = НачатьВыполнениеНаСерввере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении , ПараметрыОжидания );

КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеНаСерввере() 
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ВыполняемыйМетод = "ВКМ_ЗаполнитьСписокДоговоров.ЗаполнитьСписокДоговоров";
	ПараметрСписокДоговоров = Объект.ВКМ_СписокДоговоров.Выгрузить().ВыгрузитьКолонку("ВКМ_Договор");
	ПараметрПериод = Объект.ВКМ_Период;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ВыполняемыйМетод, ПараметрСписокДоговоров, 
			ПараметрПериод);
				
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
			
	Если Результат.Статус = "Ошибка" Тогда
		
		Сообщение = Новый  СообщениеПользователю();
		Сообщение.Текст = СтрШаблон("Ошибка! Описание ошибки : %1.", Результат.КраткоеПредставлениеОшибки);
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	//ВывестиРезельтат(Результат.АдресРезультата);
	
	СообщениеПользователю = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеПользователю);		
	
КонецПроцедуры



