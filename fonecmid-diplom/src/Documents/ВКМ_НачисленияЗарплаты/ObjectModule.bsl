
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвиженияОсновныеНачисления();
	
	РассчитатьОклад();
	
	РасчитатьОтпуск();
		
КонецПроцедуры

Процедура СформироватьДвиженияОсновныеНачисления()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_ВидРасчета КАК ВидРасчета,
	|	ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_ДатаНачала КАК ДатаНачала,
	|	ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ТЧ
	|ИЗ
	|	Документ.ВКМ_НачисленияЗарплаты.ВКМ_Начисления КАК ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.Ссылка = &Ссылка
	|	И (ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_ВидРасчета = &Оклад
	|	ИЛИ ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_ВидРасчета = &Отпуск)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ.Сотрудник,
	|	ВТ_ТЧ.ВидРасчета,
	|	ВТ_ТЧ.ДатаНачала,
	|	ВТ_ТЧ.ДатаОкончания,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад КАК Оклад
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, ВКМ_Сотрудник В
	|		(ВЫБРАТЬ
	|			ВТ_ТЧ.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТ_ТЧ КАК ВТ_ТЧ)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТЧ КАК ВТ_ТЧ
	|		ПО ВТ_ТЧ.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник";	
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка );
	Запрос.УстановитьПараметр("Период", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад );
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск );
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВКМ_Сотрудник = Выборка.Сотрудник;		
		Движение.ВидРасчета = Выборка.ВидРасчета;
		Движение.ВКМ_Значение = Выборка.Оклад;
		Движение.ПериодДействияНачало = Выборка.ДатаНачала;
		Движение.ПериодДействияКонец = Выборка.ДатаОкончания;
		
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск Тогда
			
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Выборка.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Выборка.ДатаОкончания, -1));
			Движение.ВКМ_КалендарныхДней = (Выборка.ДатаОкончания - Выборка.ДатаНачала + 1) / 86400;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьОклад()
	
	РазмерНДФЛ = Константы.ВКМ_РазмерНДФЛ.Получить();
	
	Если Не ЗначениеЗаполнено(РазмерНДФЛ) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не установлено значение НДФЛ.";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Значение КАК Значение,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеПериодДействия КАК ЗначениеПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия,
	|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_СуммаКОплатеОборот, 0) КАК СуммаКОплате,
	|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
	|		И ВидРасчета = &Оклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ПО ВКМ_ОсновныеНачисления.ВКМ_Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,,) КАК
	|			ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|		ПО ВКМ_ОсновныеНачисления.ВКМ_Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Оклад";	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		
		Если Выборка.ЗначениеПериодДействия = Выборка.ЗначениеФактическийПериодДействия Тогда
			
			Движение.ВКМ_Результат = Выборка.Значение + Выборка.СуммаКОплате ;
			
		Иначе
			
			Если Выборка.ЗначениеПериодДействия = 0 ИЛИ Выборка.ЗначениеФактическийПериодДействия = 0  Тогда
				
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Нет данных о графике работы";
				Сообщение.Сообщить();
				
				Продолжить;
				
			КонецЕсли;
			
			СтоимостьДня = Выборка.Значение / Выборка.ЗначениеПериодДействия;
				
			Движение.ВКМ_Результат = СтоимостьДня * Выборка.ЗначениеФактическийПериодДействия + Выборка.СуммаКОплате;
			
		КонецЕсли;
		 
		РазмерУдержания = РасчитатьНДФЛ(Выборка.Сотрудник, Движение.ВКМ_Результат, РазмерНДФЛ);
		
		ДвижениеВзаиморасчеты = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		ДвижениеВзаиморасчеты.Период = Дата;
		ДвижениеВзаиморасчеты.ВидДвижения = ВидДвиженияНакопления.Приход;
		ДвижениеВзаиморасчеты.ВКМ_Сотрудник = Выборка.Сотрудник;
		ДвижениеВзаиморасчеты.ВКМ_Сумма = Движение.ВКМ_Результат - РазмерУдержания ;	
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры

Процедура РасчитатьОтпуск()

	РазмерНДФЛ = Константы.ВКМ_РазмерНДФЛ.Получить();
	
	Если Не ЗначениеЗаполнено(РазмерНДФЛ) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не установлено значение НДФЛ.";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеБазовыйПериод, 0) КАК РабочихДнейВГоду,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВКМ_РезультатБаза, 0) КАК СуммаНачисленийЗаГод,
	|	ВКМ_ОсновныеНачисления.НомерСтроки,
	|	ВКМ_ОсновныеНачисления.ВКМ_КалендарныхДней КАК КалендарныхДней,
	|	ВКМ_ОсновныеНачисления.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_ОсновныеНачисления.ПериодДействияНачало,
	|	ВКМ_ОсновныеНачисления.ПериодДействияКонец
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
	|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ПО ВКМ_ОсновныеНачисления.ВКМ_Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
	|			Регистратор = &Ссылка
	|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ПО ВКМ_ОсновныеНачисления.ВКМ_Сотрудник = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВКМ_Сотрудник
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск
	|	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск);
	
	Измерения = Новый Массив;
	Измерения.Добавить("ВКМ_Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];	
		
		Если Выборка.РабочихДнейВГоду = 0 Тогда
			
			Движение.ВКМ_Результат = 0;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Выборка.СуммаНачисленийЗаГод = 0 Тогда
			
			МРОТ = 12000;
			НачисленийЗаГод = МРОТ * 12;
			 	
			Движение.ВКМ_Результат = НачисленийЗаГод / Выборка.РабочихДнейВГоду * Выборка.КалендарныхДней ;
			
		Иначе
			
			Движение.ВКМ_Результат = Выборка.СуммаНачисленийЗаГод / Выборка.РабочихДнейВГоду * Выборка.КалендарныхДней ;
		
		КонецЕсли;
		
		РазмерУдержания = РасчитатьНДФЛ(Выборка.Сотрудник, Движение.ВКМ_Результат , РазмерНДФЛ );
		
		ДвижениеВзаиморасчеты = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		ДвижениеВзаиморасчеты.Период = Дата;
		ДвижениеВзаиморасчеты.ВидДвижения = ВидДвиженияНакопления.Приход;
		ДвижениеВзаиморасчеты.ВКМ_Сотрудник = Выборка.Сотрудник;
		ДвижениеВзаиморасчеты.ВКМ_Сумма = Движение.ВКМ_Результат - РазмерУдержания ;
		
		ДвижениеОтпуск = Движения.ВКМ_ОтпускиСотрудников.Добавить();
		ДвижениеОтпуск.Период = Выборка.ПериодДействияНачало;
		ДвижениеОтпуск.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеОтпуск.ВКМ_Сотрудник = Выборка.Сотрудник;
		ДвижениеОтпуск.ВКМ_ДатаНачала = Выборка.ПериодДействияНачало;
		ДвижениеОтпуск.ВКМ_ДатаОкончания = Выборка.ПериодДействияКонец;
		ДвижениеОтпуск.ВКМ_ДнейОтпуска = Выборка.КалендарныхДней;
			
			
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	Движения.ВКМ_ОтпускиСотрудников.Записать();
	
КонецПроцедуры

Функция РасчитатьНДФЛ(Сотрудник, СуммаОплаты, РазмерНДФЛ)
	
	Движение = Движения.ВКМ_Удержания.Добавить();
	Движение.ПериодРегистрации = Дата;
	Движение.ВКМ_Сотрудник = Сотрудник;
	Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.ВКМ_НДФЛ;
	Движение.ВКМ_Значение = РазмерНДФЛ;	
	Движение.ВКМ_Результат = СуммаОплаты * 13 /100 ;
	
	Движения.ВКМ_Удержания.Записать();
	
	Возврат Движение.ВКМ_Результат;
	
КонецФункции

//ВЫБРАТЬ
	//ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	//ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_КалендарныхДней КАК ВКМ_КалендарныхДней,
	//ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Значение КАК ВКМ_Значение,
	//ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеБазовыйПериод КАК ВКМ_ЗначениеБазовыйПериод
//ИЗ
	//РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика






