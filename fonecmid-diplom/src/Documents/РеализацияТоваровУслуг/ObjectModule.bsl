
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьАвтозаполнение() Экспорт
	
	НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	НоменклатураРаботыСпециалиста = Константы.ВКМ_НоменклатураРаботаСпециалиста.Получить();
	
	Если Не ЗначениеЗаполнено(НоменклатураАбонентскаяПлата) Или Не ЗначениеЗаполнено(НоменклатураРаботыСпециалиста) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = " Ошибка выполнения автозаполнения. Не заполнена Номенклатура";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	Если НоменклатураАбонентскаяПлата.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			
		Товары.Очистить();
	
		Если Договор.ВКМ_СуммаАбонентскойПлаты <> 0 Тогда
		
			СтрокаТЧТовары = Товары.Добавить();
			СтрокаТЧТовары.Номенклатура = НоменклатураАбонентскаяПлата;
			СтрокаТЧТовары.Сумма = Договор.ВКМ_СуммаАбонентскойПлаты;	
		
		КонецЕсли;
		
	ИначеЕсли НоменклатураАбонентскаяПлата.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
		
		Услуги.Очистить();
		
		Если Договор.ВКМ_СуммаАбонентскойПлаты <> 0 Тогда
		
			СтрокаТЧУслуги = Услуги.Добавить();
			СтрокаТЧУслуги.Номенклатура = НоменклатураАбонентскаяПлата;
			СтрокаТЧУслуги.Сумма = Договор.ВКМ_СуммаАбонентскойПлаты;	
		
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ВыполненныеКлиентуРаботы.ВКМ_Договор,
	|	СУММА(ВКМ_ВыполненныеКлиентуРаботы.ВКМ_КоличествоЧасов) КАК КоличествоЧасов,
	|	СУММА(ВКМ_ВыполненныеКлиентуРаботы.ВКМ_СуммаКОплате) КАК СуммаКОплате
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы КАК ВКМ_ВыполненныеКлиентуРаботы
	|ГДЕ
	|	ВКМ_ВыполненныеКлиентуРаботы.ВКМ_Договор = &Договор
	|	И ВКМ_ВыполненныеКлиентуРаботы.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ВыполненныеКлиентуРаботы.ВКМ_Договор";
	
	Запрос.УстановитьПараметр("Договор", Договор );
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НоменклатураРаботыСпециалиста.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда	
		
			СтрокаТЧТовары = Товары.Добавить();
			СтрокаТЧТовары.Номенклатура = НоменклатураРаботыСпециалиста;
			СтрокаТЧТовары.Количество = Выборка.КоличествоЧасов;
			СтрокаТЧТовары.Сумма = Выборка.СуммаКОплате;			
			
		ИначеЕсли НоменклатураРаботыСпециалиста.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			
			СтрокаТЧУслуги = Услуги.Добавить();
			СтрокаТЧУслуги.Номенклатура = НоменклатураРаботыСпециалиста;
			СтрокаТЧУслуги.Количество = Выборка.КоличествоЧасов;
			СтрокаТЧУслуги.Сумма = Выборка.СуммаКОплате;
		
		КонецЕсли;
		
	КонецЦикла;
	
					
КонецПроцедуры

#КонецОбласти

#КонецЕсли
